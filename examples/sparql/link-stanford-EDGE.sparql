# load OLiA Annotation Model for PTB POS annotation and the Linking Model
# this will be slow, faster would be to have a local copy at hand
# => initialize models in CoNLLStreamExtractor by providing a mapping from columns to OLiA URIs

#CREATE SILENT GRAPH <http://purl.org/olia/stanford.owl>;
#LOAD <http://purl.org/olia/stanford.owl> INTO GRAPH <http://purl.org/olia/stanford.owl>;
#LOAD <http://purl.org/olia/stanford-link.rdf> INTO GRAPH <http://purl.org/olia/stanford.owl>;

PREFIX conll: <http://ufal.mff.cuni.cz/conll2009-st/task-description.html#>
PREFIX olia: <http://purl.org/olia/olia.owl#>
PREFIX oliasys: <http://purl.org/olia/system.owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
INSERT {
	?a ?rel ?o.
	?o a ?c.
	?a ?rel ?c.
	?a ?oliaRel ?o.
	?a ?oliaRel ?c.
	?a a ?super.
} 
WHERE {
	?a conll:EDGE ?edge.
	GRAPH <http://purl.org/olia/stanford.owl> {
		{ ?x oliasys:hasTag ?edge } UNION
		{ ?x oliasys:hasTagContaining   ?edgeCont  FILTER(contains(str(?edge),str(?edgeCont))) } UNION
		{ ?x oliasys:hasTagMatching     ?edgeMatch FILTER(   regex(str(?edge),str(?edgeMatch))) } UNION
		{ ?x oliasys:hasTagStartingWith ?edgeStart FILTER(   regex(str(?edge),concat(str(?edgeStart),'.*'))) } UNION
		{ ?x oliasys:hasTagEndingWith   ?edgeEnd   FILTER(   regex(str(?edge),concat('.*',str(?edgeEnd)))) }
		{ ?x ?rel ?o FILTER(contains(concat(str(?rel)),'http://purl.org/olia/'))
			OPTIONAL { ?o a/rdfs:subClassOf* ?c FILTER(contains(str(?c),'http://purl.org/olia/')) }
			OPTIONAL { ?rel rdfs:subPropertyOf* ?oliaRel FILTER(contains(str(?oliaRel),'http://purl.org/olia/olia.owl')) }
		} UNION {
			?x a ?o FILTER(contains(concat(str(?o)),'http://purl.org/olia/')).
			?o rdfs:subClassOf* ?super FILTER(contains(concat(str(?super)),'http://purl.org/olia/')).
		}
	}
};
#DROP GRAPH <http://purl.org/olia/stanford.owl>